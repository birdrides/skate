import co.bird.Library

buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id "org.jetbrains.kotlin.jvm" version "1.8.22"
  id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

group = "co.bird.skate"
version = "0.2.2"

repositories {
  mavenCentral()
}

nexusPublishing {
  repositories {
    sonatype{
      username = System.getenv("OSSRH_USER")
      password = System.getenv("OSSRH_PASSWORD")
      nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
      snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
    }
  }
}

subprojects { p ->
  apply(plugin: "kotlin")
  apply(plugin: "java")
  apply(plugin: "maven-publish")
  apply(plugin: "signing")

  repositories {
    mavenCentral()
  }

  group = group
  version = version

  dependencies {
    implementation(Library.Kotlin.stdlib)
  }

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
  }

  kotlin {
    jvmToolchain(17)
  }
}

subprojects { p ->
  publishing {
    publications {
      mavenJava(MavenPublication) {
        from(components["java"])

        afterEvaluate {
          artifactId = p.name
          group = group
          version = version
        }

        pom {
          name = p.name
          url = "http://skateql.io"
          description = "Skate is a SQL generation and query library for Kotlin"
          licenses {
            license {
              name = "MIT"
            }
          }
          developers {
            developer {
              id = "cpoolebird"
              name = "Connor Poole"
              email = "cpoole@bird.co"
            }
          }
          scm {
            connection = "scm:git://git@github.com:birdrides/skate.git"
            url = "https://github.com/birdrides/skate"
          }
        }
      }
    }
  }

  signing {
    sign publishing.publications["mavenJava"]
  }
}

